//
@Metadata.layer: #CUSTOMER
@UI.headerInfo.description.label: 'Variants'

@UI: { headerInfo: { typeName: 'Variant',
                     typeNamePlural: 'Variants',
                     title: { type: #STANDARD, value: 'VariantId' } } }

@UI.presentationVariant: [{ sortOrder: [{ by: 'Sort', direction: #ASC }] }]

annotate view ZNEPT_QZ_C_VARIANT_D with
{
  @UI.facet: [ { id: 'Variant',
                 purpose: #STANDARD,
                 type: #IDENTIFICATION_REFERENCE,
                 label: 'Variant details',
                 position: 10 } ]

  @UI.hidden: true
  TestId;

  @UI.hidden: true
  QuestionId;

  @UI: { lineItem: [ { position: 10, label: 'Variant ID' } ],
         identification:[ { position: 10, label: 'Variant ID' } ] }
  VariantId;

  @UI: { lineItem: [ { position: 20, label: 'Variant', cssDefault: { width: '50rem' } },
                     { type: #FOR_ACTION, dataAction: 'moveVariantLast', label: 'Last' },
                     { type: #FOR_ACTION, dataAction: 'moveVariantFirst', label: 'First' },
                     { type: #FOR_ACTION, dataAction: 'moveVariantDown', label: 'Down' },
                     { type: #FOR_ACTION, dataAction: 'moveVariantUp', label: 'Up' } ],
         identification:[ { position: 20, label: 'Variant' } ] }
  Variant;

  @UI: { lineItem: [ { position: 30, label: 'Is correct answer' } ],
         identification:[ { position: 30, label: 'Is correct answer' } ] }
  @Consumption.valueHelpDefinition: [{ entity: { name: 'ZNEPT_QZ_I_VARIANT_CORRECT_VH', element: 'Correct' } }]
  Correct;

  @EndUserText.label: 'Version'
  @EndUserText.quickInfo: 'Version (mobile sync)'
  @UI: { identification:[ { position: 40, label: 'Version' } ] }
  Version;

  @UI.hidden: true
  CorrectText;
}