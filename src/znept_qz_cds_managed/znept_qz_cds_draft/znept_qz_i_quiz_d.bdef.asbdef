//
managed implementation in class zbp_nept_qz_i_quiz_d unique;
strict;
with draft;

define behavior for ZNEPT_QZ_I_QUIZ_D alias Quiz
persistent table znept_qz_tst
draft table znept_qz_tst_d
lock master
total etag ETag
authorization master ( instance )
late numbering
{
  create;
  update;
  delete;

  draft determine action Prepare;
  draft action Edit;
  draft action Activate;
  draft action Discard;
  draft action Resume;

  action ( features : instance ) Publish;
  action ( features : instance ) Unpublish;

  // readonly is still updated in IN LOCAL MODE
  field ( readonly ) TestId, UploadAt, UploadOn, UploadBy, Upload_By_Name, Part_Count, Question_Count, UI_Published, Version;
  field ( mandatory ) Description;

  determination setTestETag on modify { create; }

  // it will be calling the method on listed fields create/update and specified operation
  determination setVersion on save { field Description; }

  association _Part { create; with draft; }
  association _Question { create; with draft; }

  mapping for znept_qz_tst
  {
    TestId = test_id;

    UploadAt = upload_at;
    UploadOn = upload_on;
    UploadBy = upload_by;
    Description = description;
    Published = published;
    Version = version;
  }
}

define behavior for ZNEPT_QZ_I_PART_D alias Part
implementation in class zbp_nept_qz_i_part_d unique
persistent table znept_qz_prt
draft table znept_qz_prt_d query ZNEPT_QZ_D_PART_D
lock dependent by _Quiz
authorization dependent by _Quiz
late numbering
{
  update;
  delete;

  action ( features : instance ) movePartUp;
  action ( features : instance ) movePartDown;
  action ( features : instance ) movePartFirst;
  action ( features : instance ) movePartLast;

  // readonly is still updated in IN LOCAL MODE
  field ( readonly ) TestId, PartId, Sort, Version;
  field ( mandatory ) Description;

  // it will be calling the method on listed fields create/update and specified operation
  determination setVersion on save { delete; field Description, Sort; }
  determination setSort on save { field Sort; }

  association _Quiz { with draft; }

  mapping for znept_qz_prt
  {
    TestId = test_id;
    PartId = part_id;

    Description = description;
    Sort = sort;
    Version = version;
  }
}

define behavior for ZNEPT_QZ_I_QUESTION_D alias Question
implementation in class zbp_nept_qz_i_question_d unique
persistent table znept_qz_qst
draft table znept_qz_qst_d query ZNEPT_QZ_D_QUESTION_D
lock dependent by _Quiz
authorization dependent by _Quiz
late numbering
{
  update;
  delete;

  action ( features : instance ) moveQuestionUp;
  action ( features : instance ) moveQuestionDown;
  action ( features : instance ) moveQuestionFirst;
  action ( features : instance ) moveQuestionLast;

  action ( features : instance ) assignPart parameter ZNEPT_QZ_A_QUESTION_ASSIGN;
  action ( features : instance ) refreshQuestion;

  // readonly is still updated in IN LOCAL MODE
  field ( readonly ) TestId, PartId, QuestionId, Sort, Version;
  field ( mandatory ) Question;

  // it will be calling the method on listed fields create/update and specified operation
  determination setVersion on save { delete; field Question, Explanation, Sort, PartId; }
  determination setSort on save { field Sort; }

  association _Quiz { with draft; }
  association _Variant { create; with draft; }

  mapping for znept_qz_qst
  {
    TestId = test_id;
    QuestionId = question_id;

    PartId = part_id;
    Question = question;
    Explanation = explanation;
    Sort = sort;
    Version = version;
  }
}

define behavior for ZNEPT_QZ_I_VARIANT_D alias Variant
implementation in class zbp_nept_qz_i_variant_d unique
persistent table znept_qz_var
draft table znept_qz_var_d query ZNEPT_QZ_D_VARIANT_D
lock dependent by _Quiz
authorization dependent by _Quiz
late numbering
{
  update;
  delete;

  action ( features : instance ) moveVariantUp;
  action ( features : instance ) moveVariantDown;
  action ( features : instance ) moveVariantFirst;
  action ( features : instance ) moveVariantLast;

  // readonly is still updated in IN LOCAL MODE
  field ( readonly ) TestId, QuestionId, VariantId, Sort, Version;
  field ( mandatory ) Variant;

  // it will be calling the method on listed fields create/update and specified operation
  determination setVersion on save { delete; field Variant, Correct, Sort; }
  determination setSort on save { field Sort; }

  association _Quiz { with draft; }
  association _Question { with draft; }

  mapping for znept_qz_var
  {
    TestId = test_id;
    QuestionId = question_id;
    VariantId = variant_id;

    Variant = variant;
    Correct = correct;
    Sort = sort;
    Version = version;
  }
}