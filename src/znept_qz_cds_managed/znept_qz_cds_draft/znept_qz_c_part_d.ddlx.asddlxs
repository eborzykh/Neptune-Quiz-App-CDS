//
@Metadata.layer: #CUSTOMER
@UI.headerInfo.description.label: 'Parts'

@UI: { headerInfo: { typeName: 'Part',
                     typeNamePlural: 'Parts' ,
                     title: { type: #STANDARD, value: 'PartId' } } }

@UI.presentationVariant: [{ sortOrder: [{ by: 'Sort', direction: #ASC }] }]

annotate view ZNEPT_QZ_C_PART_D with
{
  @UI.facet: [ { id: 'Parts',
                 purpose: #STANDARD,
                 type: #IDENTIFICATION_REFERENCE,
                 label: 'Description',
                 position: 10 },

               { id: 'Questions',
                 type: #LINEITEM_REFERENCE,
                 label: 'Questions',
                 position: 20,
                 targetElement: '_Question' } ]

  @UI.hidden: true
  TestId;

  @UI: { lineItem: [ { position: 10, label: 'Part ID' },
                     { type: #FOR_ACTION, dataAction: 'movePartLast', label: 'Last' },
                     { type: #FOR_ACTION, dataAction: 'movePartFirst', label: 'First' },
                     { type: #FOR_ACTION, dataAction: 'movePartDown', label: 'Down' },
                     { type: #FOR_ACTION, dataAction: 'movePartUp', label: 'Up' } ],
         identification:[ { position: 10, label: 'Part ID' } ] }
  PartId;

  @UI: { lineItem: [ { position: 20, label: 'Description', cssDefault: { width: '40rem' } } ],
         identification: [ { position: 20, label: 'Part Description' } ] }
  Description;

  @EndUserText.label: 'Version'
  @EndUserText.quickInfo: 'Version (mobile sync)'
  @UI: { identification: [ { position: 30, label: 'Version' } ] }
  Version;
}