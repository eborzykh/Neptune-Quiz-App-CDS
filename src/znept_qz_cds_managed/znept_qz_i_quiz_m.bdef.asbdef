//
managed implementation in class zbp_nept_qz_i_quiz_m unique;
strict ( 1 );

define behavior for ZNEPT_QZ_I_QUIZ_M alias Quiz
implementation in class zbp_nept_qz_i_quiz_m unique
persistent table znept_qz_tst
with additional save
etag master ETag
lock master
late numbering
authorization master ( global )
{
  create;
  update;
  delete;

  action ( features : instance ) Publish;
  action ( features : instance ) Unpublish;

  field ( readonly ) TestId, UploadAt, UploadOn, UploadBy, Upload_By_Name, Part_Count, Question_Count, UI_Published, Version;
  field ( mandatory ) Description;

  determination setTestETag on modify { create; }

  // it will be calling the method on listed fields create/update and specified operation
  determination setVersion on save { field Description; }

  association _Part { create; }
  association _Question { create; }

  mapping for znept_qz_tst
  {
    TestId = test_id;

    UploadAt = upload_at;
    UploadOn = upload_on;
    UploadBy = upload_by;
    Description = description;
    Published = published;
    Version = version;
  }
}

define behavior for ZNEPT_QZ_I_PART_M alias Part
implementation in class zbp_nept_qz_i_part_m unique
with additional save
persistent table znept_qz_prt
lock dependent by _Quiz
authorization dependent by _Quiz
late numbering
{
  update;
  delete;

  action ( features : instance ) movePartUp;
  action ( features : instance ) movePartDown;
  action ( features : instance ) movePartFirst;
  action ( features : instance ) movePartLast;

  field ( readonly ) TestId, PartId, Version;
  field ( mandatory ) Description;

  // it will be calling the method on listed fields create/update and specified operation
  determination setVersion on save { delete; field Description, Sort; }
  determination setSort on save { field Sort; }

  association _Quiz;

  mapping for ZNEPT_QZ_PRT
  {
    TestId = test_id;
    PartId = part_id;

    Sort = sort;
    Description = description;
    Version = version;
  }
}

define behavior for ZNEPT_QZ_I_QUESTION_M alias Question
implementation in class zbp_nept_qz_i_question_m unique
with additional save
persistent table znept_qz_qst
lock dependent by _Quiz
authorization dependent by _Quiz
late numbering
{
  update;
  delete;

  action ( features : instance ) moveQuestionUp;
  action ( features : instance ) moveQuestionDown;
  action ( features : instance ) moveQuestionFirst;
  action ( features : instance ) moveQuestionLast;

  action ( features : instance ) assignPart parameter ZNEPT_QZ_A_QUESTION_ASSIGN;
  action ( features : instance ) refreshQuestion;

  field ( readonly ) TestId, PartId, QuestionId, Version;
  field ( mandatory ) Question;

  // it will be calling the method on listed fields create/update and specified operation
  determination setVersion on save { delete; field Question, Explanation, Sort, PartId; }
  determination setSort on save { field Sort; }

  association _Quiz;
  association _Variant { create; }

  mapping for ZNEPT_QZ_QST
  {
    TestId = test_id;
    QuestionId = question_id;

    Sort = sort;
    PartId = part_id;
    Question = question;
    Explanation = explanation;
    Version = version;
  }
}

define behavior for ZNEPT_QZ_I_VARIANT_M alias Variant
implementation in class zbp_nept_qz_i_variant_m unique
with additional save
persistent table znept_qz_var
lock dependent by _Quiz
authorization dependent by _Quiz
late numbering
{
  update;
  delete;

  action ( features : instance ) moveVariantUp;
  action ( features : instance ) moveVariantDown;
  action ( features : instance ) moveVariantFirst;
  action ( features : instance ) moveVariantLast;

  field ( readonly ) TestId, QuestionId, VariantId, Version;
  field ( mandatory ) Variant;

  // it will be calling the method on listed fields create/update and specified operation
  determination setVersion on save { delete; field Variant, Correct, Sort; }
  determination setSort on save { field Sort; }

  association _Quiz;
  association _Question;

  mapping for znept_qz_var
  {
    TestId = test_id;
    QuestionId = question_id;
    VariantId = variant_id;

    Sort = sort;
    Variant = variant;
    Correct = correct;
    Version = version;
  }
}