@Metadata.layer: #CUSTOMER
@UI.headerInfo.description.label: 'Quiz'

annotate view ZNEPT_QZ_C_TST with
{
  @UI.facet: [
  {
    label:    'Header',
    id:       'header',
    purpose:  #STANDARD,
    type:     #COLLECTION,
    position: 10
  },
    {
      label: 'General Attributes',
      id: 'attributes',
      purpose:  #STANDARD,
      parentId: 'header',
      targetQualifier : 'attributes',
      type : #FIELDGROUP_REFERENCE,
      position: 10
    },
    {
      label: 'Upload Details',
      id: 'upload_details',
      purpose:  #STANDARD,
      parentId: 'header',
      targetQualifier : 'upload_details',
      type : #FIELDGROUP_REFERENCE,
      position: 20
    },
    {
      label: 'Counts',
      id: 'count_details',
      purpose:  #STANDARD,
      parentId: 'header',
      targetQualifier : 'count_details',
      type : #FIELDGROUP_REFERENCE,
      position: 30
    },
  {
    label: 'Parts',
    id: 'parts',
    purpose:  #STANDARD,
    type: #LINEITEM_REFERENCE,
    position: 20,
    targetElement: '_Parts',
    hidden: #( UI_Hide_Parts )
  },
  {
    label: 'Questions',
    id: 'questions',
    purpose:  #STANDARD,
    type: #LINEITEM_REFERENCE,
    position: 30,
    targetElement: '_Questions',
    hidden: #( UI_Hide_Questions )
  }
  ]
  
  @UI: {
    lineItem: [ { position: 10, label: 'Name' } ],
    fieldGroup: [{ qualifier: 'attributes', position: 10, label: 'Name' }]
  }
  @Search.defaultSearchElement: true
  Description;

  @UI: {
  lineItem: [ { position: 20, label: 'Uploaded on' } ],
  fieldGroup: [{ qualifier: 'upload_details', position: 10, label: 'Uploaded on' }]
  }
  Upload_On;

  @UI: {
   lineItem: [ { position: 30, label: 'Uploaded at' } ],
   fieldGroup: [{ qualifier: 'upload_details', position: 20, label: 'Uploaded at' }]
   }
  Upload_At;

  @UI: {
   lineItem: [ { position: 40, label: 'Uploaded by' } ],
   fieldGroup: [{ qualifier: 'upload_details', position: 30, label: 'Uploaded by' }]
   }
  Upload_By_Name;

  @EndUserText.label: 'Visibility'
  @EndUserText.quickInfo: 'Visible for all users'
  @UI: {
   lineItem: [ { position: 50, label: 'Visibility ' },
               { type: #FOR_ACTION, dataAction: 'Publish', label: 'Publish' },
               { type: #FOR_ACTION, dataAction: 'Unpublish', label: 'Unpublish' }
             ],
   fieldGroup: [ { qualifier: 'attributes', position: 20 } ]
   }
  UI_Published;

  @EndUserText.label: 'Parts Count'
  @EndUserText.quickInfo: 'Number of Parts in this Quiz'
  @UI: {
   lineItem: [ { exclude: true, position: 60, label: 'Parts' }  ],
  fieldGroup: [{ qualifier: 'count_details', position: 10, label: 'Parts' }]
  }
  Parts_Count;

  @EndUserText.label: 'Questions Count'
  @EndUserText.quickInfo: 'Number of Questions in this Quiz'
  @UI: {
  lineItem: [ { exclude: true, position: 70, label: 'Questions'  } ],
  fieldGroup: [{ qualifier: 'count_details', position: 20, label: 'Questions' }]
  }
  Questions_Count;

  @UI.hidden: true
  Test_ID;

  @UI.hidden: true
  Published;

  @UI.hidden: true
  Upload_By;

  @UI.hidden: true
  UI_Hide_Parts;

  @UI.hidden: true
  UI_Hide_Questions;

}